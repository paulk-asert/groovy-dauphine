import groovy.sql.Sql
import java.util.stream.Collectors

Sql.withInstance('jdbc:duckdb:', 'org.duckdb.DuckDBDriver') { sql ->
    println "Country  Riders                                  Places    Count"
    sql.eachRow("""SELECT Country,
        rpad(string_agg(Rider, ', '), 40, ' ') as Riders,
        rpad(string_agg(Place, ', '), 10, ' ') as Places,
        bar(count(Country), 0, 4, 30) as Count
        FROM 'src/main/groovy/topten.csv' GROUP BY Country""") { row ->
        row.with {
            println "$Country       $Riders$Places$Count"
        }
    }
}

/*
Country  Riders                                  Places    Count
ðŸ‡©ðŸ‡°       JONAS VINGEGAARD                        1         â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–Œ
ðŸ‡¬ðŸ‡§       ADAM YATES                              2         â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–Œ
ðŸ‡¦ðŸ‡º       BEN O'CONNOR, JAI HINDLEY, JACK HAIG    3, 4, 5   â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–Œ
ðŸ‡«ðŸ‡·       GUILLAUME MARTIN, JULIAN ALAPHILIPPE    6, 10     â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆ
ðŸ‡¿ðŸ‡¦       LOUIS DU BOUISSON MEINTJES              7         â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–Œ
ðŸ‡³ðŸ‡´       TORSTEIN TRÃ†EN                          8         â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–Œ
ðŸ‡ªðŸ‡¸       CARLOS RODRIGUEZ CANO                   9         â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–Œ
*/

var f = 'src/main/groovy/topten.csv' as File
var lines = f.readLines()*.split(',')
var cols = lines[0].size()
var rows = lines[1..-1].collect{row ->
    (0..<cols).collectEntries{ col -> [lines[0][col], row[col]] }}
var commaDelimited = Collectors.joining(', ')
var aggRiders = { it.stream().map(rec -> rec.r.Rider).collect(commaDelimited) }
var aggPlaces = { it.stream().map(rec -> rec.r.Place).collect(commaDelimited) }
println GQ {
    from r in rows
    groupby r.Country
    select r.Country,
        agg(aggRiders(_g)) as Riders,
        agg(aggPlaces(_g)) as Places,
        'â–ˆâ–ˆ' * count(r.Country) as Count
}

/*
+---------+--------------------------------------+---------+--------+
| Country | Riders                               | Places  | Count  |
+---------+--------------------------------------+---------+--------+
| ðŸ‡¦ðŸ‡º      | BEN O'CONNOR, JAI HINDLEY, JACK HAIG | 3, 4, 5 | â–ˆâ–ˆâ–ˆâ–ˆâ–ˆâ–ˆ |
| ðŸ‡©ðŸ‡°      | JONAS VINGEGAARD                     | 1       | â–ˆâ–ˆ     |
| ðŸ‡³ðŸ‡´      | TORSTEIN TRÃ†EN                       | 8       | â–ˆâ–ˆ     |
| ðŸ‡¿ðŸ‡¦      | LOUIS DU BOUISSON MEINTJES           | 7       | â–ˆâ–ˆ     |
| ðŸ‡¬ðŸ‡§      | ADAM YATES                           | 2       | â–ˆâ–ˆ     |
| ðŸ‡ªðŸ‡¸      | CARLOS RODRIGUEZ CANO                | 9       | â–ˆâ–ˆ     |
| ðŸ‡«ðŸ‡·      | GUILLAUME MARTIN, JULIAN ALAPHILIPPE | 6, 10   | â–ˆâ–ˆâ–ˆâ–ˆ   |
+---------+--------------------------------------+---------+--------+
*/
